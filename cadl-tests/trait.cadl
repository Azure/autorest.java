import "@azure-tools/cadl-azure-core";
import "@azure-tools/cadl-ranch-expect";
import "@cadl-lang/rest";
import "@cadl-lang/versioning";

using global.Azure.Core;
using global.Azure.Core.Traits;
using Cadl.Rest;
using Cadl.Http;
using Cadl.Versioning;

#suppress "@azure-tools/cadl-azure-core/casing-style" "For spec"
@doc("Illustrates Azure Core operation customizations by traits")
@scenarioService("/azure/traits")
@versioned(_Specs_.Azure.Traits.Versions)
namespace _Specs_.Azure.Traits;

enum Versions {
  @useDependency(global.Azure.Core.Versions.v1_0_Preview_2)
  v2022_12_01_preview: "2022-12-01-preview",
}

alias ServiceTraits = SupportsRepeatableRequests & SupportsConditionalRequests & SupportsClientRequestId;

alias OperationsWithTraits = global.Azure.Core.ResourceOperations<ServiceTraits>;

@doc("Sample Model")
@resource("user")
model User {
  @key
  @doc("The user's id.")
  @visibility("read")
  id: int32;

  @doc("The user's name.")
  name?: string;
}

@doc("The ApiVersion path parameter.")
model ApiVersionPathParameter {
  @segment("api")
  @path("api-version")
  @doc("The API version to use for this operation.")
  apiVersion: string;
}

@scenario
@doc("Get a resource, sending and receiving headers.")
@scenarioDoc("""
Expected path parameter: id=1
Expected query parameter: api-version=2022-12-01-preview
Expected header parameters:
- foo=123
- if-match=valid
- if-none-match=invalid
- if-unmodified-since=2022-08-26T18:38:00.000Z
- if-modified-since=2021-08-26T18:38:00.000Z
- x-ms-client-request-id=<any string>

Expected response header: x-ms-client-request-id=<any string>
Expected response body:
```json
{
  "id": 1,
  "name": "Madge",
  "ETag": "11bdc430-65e8-45ad-81d9-8ffa60d55b59"
}
```
""")
op get is OperationsWithTraits.ResourceRead<
  User,
  RequestHeadersTrait<{
    @doc("header in request")
    @header
    foo: string;
  }> &
    ResponseHeadersTrait<{
      @header bar: string;
    }>
>;

@scenario
@doc("Delete resource with api-version path parameter")
@scenarioDoc("""
Expected path parameter:
- id=1
- api-version=2022-12-01-preview
Expected header parameters:
- x-ms-client-request-id=<any string>

Expected response headers:
- x-ms-client-request-id=<any string>
- Repeatability-Result=Accepted
""")
op delete is OperationsWithTraits.ResourceDelete<User, TraitOverride<VersionParameterTrait<ApiVersionPathParameter>>>;

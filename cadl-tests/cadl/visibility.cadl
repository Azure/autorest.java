import "@cadl-lang/rest";
import "@azure-tools/cadl-dpg";

using Cadl.Http;
using Azure.DPG;

@service({
  title: "Visibility",
  version: "1.0.0",
})
namespace Cadl.Visibility;

model Dog {
  @visibility("read") id: int32;
  @visibility("write") secretName: string;
  // no flags are like specifying all flags at once, so in this case
  // equivalent to @visibility("read", "write")
  name: string;
}

// The spread operator will copy all the properties of Dog into ReadDog,
// and withVisibility will remove any that don't match the current
// visibility setting
@withVisibility("read")
model ReadDog {
  ...Dog;
}

@withVisibility("write")
model WriteDog {
  ...Dog;
}

@operationGroup
interface VisibilityRead {
  @get
  @route("/read")
  get(): Dog;
}

@operationGroup
interface VisibilityWrite {
  @put
  @route("/write")
  create(@body dog: WriteDog): Dog;
}

@route("/visibility")
interface VisibilityOp extends VisibilityRead, VisibilityWrite {
  @post
  @route("/query")
  query(@body dog: ReadDog): Dog;
}

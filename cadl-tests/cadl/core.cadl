import "@azure-tools/cadl-azure-core";
import "@cadl-lang/rest";

using Azure.Core;
using global.Azure.Core.Traits;
using Cadl.Rest;
using Cadl.Http;

#suppress "@azure-tools/cadl-azure-core/casing-style" "For spec"
@doc("Illustrates bodies templated with Azure Core")
@service({
  title: "EnumService",
  version: "1.0.0",
})
@server("http://localhost:3000", "TestServer endpoint")
@route("/azure/core")
namespace _Specs_.Azure.Core;

interface ResourceOperations
  extends global.Azure.Core.ResourceOperations<NoConditionalRequests & NoRepeatableRequests & NoClientRequestId> {}

@resource("users")
@doc("Details about a user.")
model User {
  @key
  @doc("The user's id.")
  @visibility("read")
  id: int32;

  @doc("The user's name.")
  name: string;
}

@doc("The parameters for exporting a user.")
model UserExportParams {
  @query
  @doc("The format of the data.")
  format: string;
}

op createOrUpdate is ResourceOperations.ResourceCreateOrUpdate<User>;

op createOrReplace is ResourceOperations.ResourceCreateOrReplace<User>;

op get is ResourceOperations.ResourceRead<User>;

op list is ResourceOperations.ResourceList<User>;

op delete is ResourceOperations.ResourceDelete<User>;

op export is ResourceOperations.ResourceAction<User, UserExportParams, User>;

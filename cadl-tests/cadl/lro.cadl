import "@cadl-lang/rest";
import "@azure-tools/cadl-azure-core";

using Cadl.Http;
using Cadl.Rest;
using Azure.Core;

@serviceTitle("LongRunning")
@serviceVersion("1.0.0")
namespace Cadl.LongRunning;

model Resource {
  @visibility("read")
  id: string;

  @key
  @segment("resources")
  name: string;
  type: string;
}

model ExportParams {
  @query
  projectFileVersion: string;
}

model ExportedResource {
  id: string;
  resourceUri: string;
}

@route("/long-running")
interface LongRunningOp {
  statusMonitor is CustomCore.PollingOperation<Resource>;

  @pollingOperation(LongRunningOp.statusMonitor)
  createOrUpdate is LongRunningResourceCreateOrUpdate<Resource>;

  get is ResourceRead<Resource>;

  @pollingOperation(LongRunningOp.statusMonitor)
  delete is LongRunningResourceDelete<Resource>;

  @pollingOperation(LongRunningOp.statusMonitor)
  export is ResourceAction<Resource, ExportParams, Cadl.Http.AcceptedResponse & Foundations.LongRunningStatusLocation>;

  @pollingOperation(LongRunningOp.statusMonitor)
  importx is ResourceAction<
    Resource,
    ExportedResource,
    Cadl.Http.AcceptedResponse & Foundations.LongRunningStatusLocation
  >;
}

namespace CustomCore {
  @parentResource(T)
  @resource("operations")
  model OperationStatusResource<T> is Azure.Core.Foundations.OperationStatus<T>;

  @route("operations")
  op PollingOperation<TCustom> is Azure.Core.ResourceRead<OperationStatusResource<TCustom>>;
}

import "@cadl-lang/rest";
import "@azure-tools/cadl-azure-core";

using Cadl.Http;
using Cadl.Rest;
using Cadl.Versioning;
using Azure.Core;
using Azure.Core.Foundations;
using Azure.Core.Traits;

@service({
  title: "LongRunning",
  version: "1.0.0",
})
@versionedDependency(Azure.Core.Versions.v1_0_Preview_2)
namespace Cadl.LongRunning;

model Resource {
  @visibility("read")
  id: string;

  @key
  @segment("resources")
  name: string;
  type: string;
}

model ExportParams {
  @query
  projectFileVersion: string;
}

model ExportedResource {
  id: string;
  resourceUri: string;
}

interface ResourceOperations
  extends Azure.Core.ResourceOperations<NoRepeatableRequests & NoConditionalRequests & NoClientRequestId> {}

@route("/long-running")
interface LongRunningOp {
  statusMonitor is CustomCore.PollingOperation<Resource>;

  @pollingOperation(LongRunningOp.statusMonitor)
  createOrUpdate is ResourceOperations.LongRunningResourceCreateOrUpdate<Resource>;

  @pollingOperation(LongRunningOp.statusMonitor)
  createOrReplace is ResourceOperations.LongRunningResourceCreateOrReplace<Resource>;

  get is ResourceOperations.ResourceRead<Resource>;

  @pollingOperation(LongRunningOp.statusMonitor)
  delete is ResourceOperations.LongRunningResourceDelete<Resource>;

  @pollingOperation(LongRunningOp.statusMonitor)
  export is ResourceOperations.ResourceAction<
    Resource,
    ExportParams,
    Cadl.Http.AcceptedResponse & LongRunningStatusLocation
  >;

  @pollingOperation(LongRunningOp.statusMonitor)
  importx is ResourceOperations.ResourceAction<
    Resource,
    ExportedResource,
    Cadl.Http.AcceptedResponse & LongRunningStatusLocation
  >;
}

namespace CustomCore {
  @parentResource(T)
  @resource("operations")
  model OperationStatusResource<T> is OperationStatus<T>;

  @route("operations")
  op PollingOperation<TCustom> is ResourceOperations.ResourceRead<OperationStatusResource<TCustom>>;
}

import "@cadl-lang/rest";
import "@azure-tools/cadl-azure-core";

using Cadl.Http;
using Cadl.Rest;
using Cadl.Versioning;
using Azure.Core;
using Azure.Core.Foundations;
using Azure.Core.Traits;

@service({
  title: "LongRunning",
  version: "1.0.0",
})
@useDependency(Azure.Core.Versions.v1_0_Preview_2)
namespace Cadl.LongRunning;

model Resource {
  @visibility("read")
  id: string;

  @key
  @segment("resources")
  name: string;
  type: string;
}

model ExportParams {
  @query
  projectFileVersion: string;
}

model ExportedResource {
  id: string;
  resourceUri: string;
}

interface ResourceOperations
  extends Azure.Core.ResourceOperations<NoRepeatableRequests & NoConditionalRequests & NoClientRequestId> {}

@route("/long-running")
interface LongRunningOp {
  createOrUpdate is ResourceOperations.ResourceCreateOrUpdate<Resource>;

  @pollingOperation(CustomCore.pollingOperation<Resource>)
  createOrReplace is ResourceOperations.LongRunningResourceCreateOrReplace<Resource>;

  get is ResourceOperations.ResourceRead<Resource>;

  @pollingOperation(CustomCore.pollingOperation<Resource>)
  delete is ResourceOperations.LongRunningResourceDelete<Resource>;

  @pollingOperation(CustomCore.pollingOperation<ExportedResource>)
  export is ResourceOperations.LongRunningResourceAction<Resource, ExportParams, ExportedResource>;
}

namespace CustomCore {
  @parentResource(T)
  @resource("operations")
  @friendlyName("{name}Status", T)
  model OperationStatusResource<T> is OperationStatus<T>;

  @route("operations")
  op pollingOperation<TCustom> is ResourceOperations.ResourceRead<OperationStatusResource<TCustom>>;
}
